// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * Implements the Snake game.
 * The 4 arrow keys are used to move the Snake up, down, left, and right.
 * the Snake's size. The 'q' key is used to quit the game.
 */
class SnakeGame {
   field Snake snake; // the Snake of this game
   field int direction; // the Snake's current direction
   field Array moveVectors;

   static int SCREEN_WIDTH;
   static int SCREEN_HEIGHT;

   static int DIR_NONE;
   static int DIR_LEFT;
   static int DIR_UP;
   static int DIR_RIGHT;
   static int DIR_DOWN;

   static char KEY_Q;
   static char KEY_LEFT;
   static char KEY_UP;
   static char KEY_RIGHT;
   static char KEY_DOWN;

   /** Constructs a new Snake game. */
   constructor SnakeGame new() {
      let SCREEN_WIDTH = 510;
      let SCREEN_HEIGHT = 254;
      
      let DIR_NONE = 0;
      let DIR_LEFT = 1;
      let DIR_UP = 2;
      let DIR_RIGHT = 3;
      let DIR_DOWN = 4;

      let KEY_Q = 140;
      let KEY_LEFT = 130;
      let KEY_UP = 131;
      let KEY_RIGHT = 132;
      let KEY_DOWN = 133;

      let snake = Snake.new(Math.divide(SCREEN_WIDTH, 2), Math.divide(SCREEN_HEIGHT, 2));
      let direction = DIR_NONE;

      let moveVectors = Array.new(5);
      let moveVectors[DIR_NONE] = Vector2.new(0, 0);
      let moveVectors[DIR_LEFT] = Vector2.new(-1, 0);
      let moveVectors[DIR_UP] = Vector2.new(0, -1);
      let moveVectors[DIR_RIGHT] = Vector2.new(1, 0);
      let moveVectors[DIR_DOWN] = Vector2.new(0, 1);

      return this;
   }

   /** Disposes this game. */
   method void dispose() {
      do snake.dispose();
      do Memory.deAlloc(this);
      return;
   }

   /** Moves the Snake in the current direction. */
   method void moveSnake() {
      var Vector2 moveVec;
      let moveVec = moveVectors[direction];
      do snake.move(moveVec);
      do Sys.wait(5);  // delays the next movement
      return;
   }

   /** Runs the game: handles the user's inputs and moves the Snake accordingly */
   method void run() {
      var char key;  // the key currently pressed by the user
      var boolean exit;
      let exit = false;
      
      while (~exit) {
         // waits for a key to be pressed
         while (key = 0) {
            let key = Keyboard.keyPressed();
            let direction = DIR_NONE;
            do moveSnake();
         }

         if (key = KEY_Q)
         {
            let exit = true;
         }
         else
         {
            if((key > (KEY_LEFT - 1)) & (key < (KEY_DOWN + 1)))
            {
               let direction = key - KEY_LEFT + 1;
            }
         }

         // waits for the key to be released
         while (~(key = 0)) {
            let key = Keyboard.keyPressed();
            do moveSnake();
         }
     } // while
     return;
   }
}



